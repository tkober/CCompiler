%{

#include <stdio.h>

#import "CCParser.h"
#import "CCScanner.h"
#import "CCSymbolTable.h"
#import "CCSyntaxTree.h"
#import "CCNodes.h"
#import "CCDebug.h"

#if BISON_DEBUG
    #define YYDEBUG 1
#endif

id<CCOutput> _output;

%}

%union {
    __unsafe_unretained id syntax_node;
}

// Type Specifiers
%token VOID
%token CHAR
%token SHORT
%token INT
%token LONG
%token FLOAT
%token DOUBLE
%token SIGNED
%token UNSIGNED

// Symbols
%token <syntax_node>ID
%token STRING
%token CHAR_CONST
%token INT_CONST
%token FLOAT_CONST

// Assignement Operators
%token ASSIGN_OP
%token ASSIGN_MUL_OP
%token ASSIGN_DIV_OP
%token ASSIGN_MOD_OP
%token ASSIGN_ADD_OP
%token ASSIGN_SUB_OP
%token ASSIGN_SHIFT_LEFT_OP
%token ASSIGN_SHIFT_RIGHT_OP
%token ASSIGN_BITWISE_AND_OP
%token ASSIGN_BITWISE_XOR_OP
%token ASSIGN_BITWISE_OR_OP

// Other Operators
%token UNARY_INCREMENT_OP
%token UNARY_DECREMENT_OP
%token BITWISE_NOT_OP
%token MUL_OP
%token DIV_OP
%token ADD_OP
%token SUB_OP
%token NOT_OP
%token MOD_OP
%token AND_OP
%token OR_OP
%token LOGICAL_OR_OP
%token LOGICAL_AND_OP
%token EXCLUSIVE_OR_OP


// Comparison Operator
%token EQUAL_OP
%token NOT_EQUAL_OP
%token LT_OP
%token GT_OP
%token LE_OP
%token GE_OP

// Shift Operators
%token SHIFT_LEFT_OP
%token SHIFT_RIGHT_OP

// Punctations
%token LINE_END
%token COLON
%token COMMA
%token QUESTION_MARK

// Braces
%token OB
%token CB
%token OB_CURLY
%token CB_CURLY

//Keywords
%token BREAK
%token CONTINUE
%token WHILE
%token RETURN
%token IF
%token ELSE
%token GOTO


%type <syntax_node> translation_unit;
%type <syntax_node> external_declaration;
%type <syntax_node> function_definition;
%type <syntax_node> declaration;
%type <syntax_node> declaration_specification;
%type <syntax_node> declarator;
%type <syntax_node> declaration_list;
%type <syntax_node> compound_statement;
%type <syntax_node> init_declarator_list;
%type <syntax_node> statement_list;
%type <syntax_node> statement;
%type <syntax_node> labeled_statement;
%type <syntax_node> expression_statement;
%type <syntax_node> selection_statement;
%type <syntax_node> iteration_statement;
%type <syntax_node> jump_statement;
%type <syntax_node> expression;
%type <syntax_node> type_specification;
%type <syntax_node> init_declarator;
%type <syntax_node> initializer;
%type <syntax_node> parameter_list;
%type <syntax_node> parameter_declaration;

%nonassoc CB
%nonassoc ELSE

%%


translation_unit            : external_declaration                      { 
                                                                            [[CCSyntaxTree sharedInstance] addTranslationUnit:[CCTranslationUnitNode translationUnitWith:nil 
                                                                                                                                                         translationUnit:$1]]; 
                                                                        }
                            | translation_unit external_declaration     { 
                                                                            [[CCSyntaxTree sharedInstance] addTranslationUnit:[CCTranslationUnitNode translationUnitWith:$1
                                                                                                                                                         translationUnit:$2]];
                                                                        }
                            ;


external_declaration        : function_definition                       { $$ = [CCExternalDeclarationNode functionDefinition:$1]; }
                            | declaration                               { $$ = [CCExternalDeclarationNode declaration:$1]; }
                            ;


function_definition         : declaration_specification     declarator declaration_list compound_statement { $$ = [CCFunctionDefinitionNode functionDefinitionWithDeclarationSpecification:$1 declarator:$2 declarationList:$3 compoundStatement:$4]; }
                            |                               declarator declaration_list compound_statement { $$ = [CCFunctionDefinitionNode functionDefinitionWithDeclarationSpecification:nil declarator:$1 declarationList:$2 compoundStatement:$3]; }
                            | declaration_specification     declarator                  compound_statement { $$ = [CCFunctionDefinitionNode functionDefinitionWithDeclarationSpecification:$1 declarator:$2 declarationList:nil compoundStatement:$3]; }
                            |                               declarator                  compound_statement { $$ = [CCFunctionDefinitionNode functionDefinitionWithDeclarationSpecification:nil declarator:$1 declarationList:nil compoundStatement:$2]; }
                            ;


declaration                 : declaration_specification init_declarator_list LINE_END   { 
                                                                                            $$ = [CCDeclarationNode declarationNodeWithDeclarationSpecification:$1
                                                                                                                                             initDeclaratorList:$2];
                                                                                        }
                            | declaration_specification LINE_END                        { 
                                                                                            $$ = [CCDeclarationNode declarationNodeWithDeclarationSpecification:$1
                                                                                                                                             initDeclaratorList:nil];
                                                                                        }
                            ;
                            

declaration_list            : declaration                                               {
                                                                                            $$ =  [CCDeclarationListNode declarationListNodeWithDeclarationList:nil
                                                                                                                                                    declaration:$1];
                                                                                        }
                            | declaration_list declaration                              {
                                                                                            $$ =  [CCDeclarationListNode declarationListNodeWithDeclarationList:$1
                                                                                                                                                    declaration:$2];
                                                                                        }
                            ;


compound_statement          : OB_CURLY declaration_list statement_list  CB_CURLY    {
                                                                                        $$ = [CCCompoundStatementNode compoundStatemetnNodeWithDeclarationList:$2
                                                                                                                                                 statementList:$3];
                                                                                    }
                            | OB_CURLY                  statement_list  CB_CURLY    {
                                                                                        $$ = [CCCompoundStatementNode compoundStatemetnNodeWithDeclarationList:nil
                                                                                                                                                 statementList:$2];
                                                                                    }
                            | OB_CURLY declaration_list                 CB_CURLY    {
                                                                                        $$ = [CCCompoundStatementNode compoundStatemetnNodeWithDeclarationList:$2
                                                                                                                                                 statementList:nil];
                                                                                    }
                            | OB_CURLY                                  CB_CURLY    {
                                                                                        $$ = [CCCompoundStatementNode compoundStatemetnNodeWithDeclarationList:nil
                                                                                                                                                 statementList:nil];
                                                                                    }
                            ;


statement_list              : statement                 { 
                                                            $$ = [CCStatementListNode statementListNodeWithStatementList:nil 
                                                                                                               statement:$1];
                                                        }
                            | statement_list statement  { 
                                                            $$ = [CCStatementListNode statementListNodeWithStatementList:$1
                                                                                                               statement:$2];
                                                        }
                            ;
           

statement                   : labeled_statement         { $$ = [CCStatementNode statementNodeWithLabeledStatement:$1]; }
                            | expression_statement      { $$ = [CCStatementNode statementNodeWithExpressionStatement:$1]; }
                            | compound_statement        { $$ = [CCStatementNode statementNodeWithCompoundStatement:$1]; }
                            | selection_statement       { $$ = [CCStatementNode statementNodeWithSelectionStatement:$1]; }
                            | iteration_statement       { $$ = [CCStatementNode statementNodeWithIterationStatement:$1]; }
                            | jump_statement            { $$ = [CCStatementNode statementNodeWithJumpStatement:$1]; }
                            ;
                            

labeled_statement           : ID COLON statement        {
                                                            $$ = [CCLabeledStatementNode labeledExpressionNodeWithStatement:$1
                                                                                                                 identifier:$3];
                                                        }
                            ;


expression_statement        : expression LINE_END       { $$ = [CCExpressionStatementNode expressionStatementNodeWithExpression:$1]; }
                            | LINE_END                  { $$ = [CCExpressionStatementNode expressionStatementNodeWithExpression:nil]; }
                            ;


selection_statement         : IF OB expression CB statement                 {
                                                                                $$ = [CCSelectionStatementNode selectionStatementNodeWithExpression:$3
                                                                                                                                      thenStatement:$5
                                                                                                                                      elseStatement:nil];
                                                                            }
                            | IF OB expression CB statement ELSE statement  {
                                                                                $$ = [CCSelectionStatementNode selectionStatementNodeWithExpression:$3
                                                                                                                                      thenStatement:$5
                                                                                                                                      elseStatement:$7];
                                                                            }
                            ;


iteration_statement         : WHILE OB expression CB statement  {   
                                                                    $$ = [CCIterationStatementNode iterationStatementWithExpression:$3
                                                                                                                          statement:$5];
                                                                }
                            ;


jump_statement              : GOTO ID LINE_END              { $$ = [CCJumpStatementNode gotoStatementWithIdentifier:$2]; }
                            | CONTINUE LINE_END             { $$ = [CCJumpStatementNode continueStatement]; }
                            | BREAK LINE_END                { $$ = [CCJumpStatementNode breakStatement]; }
                            | RETURN expression LINE_END    { $$ = [CCJumpStatementNode returnStatementWithExpression:$2]; }
                            | RETURN LINE_END               { $$ = [CCJumpStatementNode returnStatementWithExpression:nil]; }
                            ;


declaration_specification   : type_specification                            { 
                                                                                $$ = [CCDeclarationSpecificationNode declarationSpecificationNodeWithdeclarationSpecification:nil
                                                                                                                                                            typeSpecification:$1];
                                                                            }
                            | type_specification declaration_specification  { 
                                                                                $$ = [CCDeclarationSpecificationNode declarationSpecificationNodeWithdeclarationSpecification:$2
                                                                                                                                                            typeSpecification:$1];
                                                                            }
                            ;


type_specification          : VOID          { $$ = [CCTypeSpecificationNode CC_VOID]; }
                            | CHAR          { $$ = [CCTypeSpecificationNode CC_CHAR]; }
                            | SHORT         { $$ = [CCTypeSpecificationNode CC_SHORT]; }
                            | INT           { $$ = [CCTypeSpecificationNode CC_INT]; }
                            | LONG          { $$ = [CCTypeSpecificationNode CC_LONG]; }
                            | FLOAT         { $$ = [CCTypeSpecificationNode CC_FLOAT]; }
                            | DOUBLE        { $$ = [CCTypeSpecificationNode CC_DOUBLE]; }
                            | SIGNED        { $$ = [CCTypeSpecificationNode CC_SIGNED]; }
                            | UNSIGNED      { $$ = [CCTypeSpecificationNode CC_UNSIGNED]; }
                            ;


init_declarator_list        : init_declarator                               {
                                                                                $$ = [CCInitDeclaratorListNode initDeclaratorListNodeWithInitDeclaratorList:nil
                                                                                                                                             initDeclarator:$1];
                                                                            }
                            | init_declarator_list COMMA init_declarator    {
                                                                                $$ = [CCInitDeclaratorListNode initDeclaratorListNodeWithInitDeclaratorList:$1
                                                                                                                                             initDeclarator:$3];
                                                                            }
                            ;


init_declarator             : declarator                        {
                                                                    $$ = [CCInitDeclaratorNode initDeclaratorNodeWithDeclarator:$1
                                                                                                                    initializer:nil];
                                                                }
                            | declarator ASSIGN_OP initializer  {
                                                                    $$ = [CCInitDeclaratorNode initDeclaratorNodeWithDeclarator:$1
                                                                                                                    initializer:$3];
                                                                }
                            ;


declarator                  : ID                                {
                                                                    $$ = [CCDeclaratorNode declaratorNodeWithDeclarator:nil
                                                                                                             identifier:$1
                                                                                                          parameterList:nil];
                                                                }
                            | OB declarator CB                  {
                                                                    $$ = [CCDeclaratorNode declaratorNodeWithDeclarator:$2
                                                                                                             identifier:nil
                                                                                                          parameterList:nil];
                                                                }
                            | declarator OB parameter_list CB   {
                                                                    $$ = [CCDeclaratorNode declaratorNodeWithDeclarator:$1
                                                                                                             identifier:nil
                                                                                                          parameterList:$3];
                                                                }
                            | declarator OB CB                  {
                                                                    $$ = [CCDeclaratorNode declaratorNodeWithDeclarator:$1
                                                                                                             identifier:nil
                                                                                                          parameterList:nil];
                                                                }
                            ;
                            
                            
parameter_list              : parameter_declaration                         {
                                                                                $$ = [CCParameterListNode parameterListNodeWithParameterDeclaration:$1
                                                                                                                                      parameterList:nil];
                                                                            }
                            | parameter_list COMMA parameter_declaration    {
                                                                                $$ = [CCParameterListNode parameterListNodeWithParameterDeclaration:$3
                                                                                                                                      parameterList:$1];
                                                                            }
                            ;
                            
                            
parameter_declaration       : declaration_specification declarator
                            | declaration_specification
                            ;


initializer                 : assignment_expression
                            | OB_CURLY initializer_list         CB_CURLY
                            | OB_CURLY initializer_list COMMA   CB_CURLY
                            ;
                            
                            
initializer_list            : initializer
                            | initializer_list COMMA initializer
                            ;


assignment_expression       : conditional_expression
                            | unary_expression assignment_operator assignment_expression
                            ;


conditional_expression      : logical_or_expression
                            | logical_or_expression QUESTION_MARK expression COLON conditional_expression
                            ;


unary_expression            : postfix_expression
                            | prefix_operator unary_expression
                            | unary_operator
                            ;
                            

prefix_operator             : UNARY_INCREMENT_OP
                            | UNARY_DECREMENT_OP
                            ;


assignment_operator         : ASSIGN_OP
                            | ASSIGN_MUL_OP
                            | ASSIGN_DIV_OP
                            | ASSIGN_MOD_OP
                            | ASSIGN_ADD_OP
                            | ASSIGN_SUB_OP
                            | ASSIGN_SHIFT_LEFT_OP
                            | ASSIGN_SHIFT_RIGHT_OP
                            | ASSIGN_BITWISE_AND_OP
                            | ASSIGN_BITWISE_XOR_OP
                            | ASSIGN_BITWISE_OR_OP
                            ;


logical_or_expression       : logical_and_expression
                            | logical_or_expression LOGICAL_OR_OP logical_and_expression
                            ;


logical_and_expression      : inclusive_or_expression
                            | logical_and_expression LOGICAL_AND_OP inclusive_or_expression
                            ;


inclusive_or_expression     : exclusive_or_expression
                            | inclusive_or_expression  OR_OP exclusive_or_expression
                            ;


exclusive_or_expression     : and_expression
                            | exclusive_or_expression EXCLUSIVE_OR_OP and_expression
                            ;


and_expression              : equality_expression
                            | and_expression AND_OP equality_expression
                            ;


equality_expression         : relational_expression
                            | equality_expression equality_operator relational_expression
                            ;
                            
                            
equality_operator           : EQUAL_OP
                            | NOT_EQUAL_OP
                            ;


relational_expression       : shift_expression
                            | relational_expression compare_operator shift_expression
                            ;
                            
                            
compare_operator            : LT_OP
                            | GT_OP
                            | LE_OP
                            | GE_OP
                            ;


shift_expression            : additive_expression
                            | shift_expression shift_operator additive_expression
                            ;
                            
                            
shift_operator              : SHIFT_LEFT_OP
                            | SHIFT_RIGHT_OP
                            ;


additive_expression         : multiplicative_expression
                            | additive_expression additive_operator multiplicative_expression
                            ;
                            
                            
additive_operator           : ADD_OP
                            | SUB_OP
                            ;


multiplicative_expression   : unary_expression
                            | multiplicative_expression multiplicative_operator unary_expression
                            ;
                            
                            
multiplicative_operator     : MUL_OP
                            | DIV_OP
                            | MOD_OP
                            ;


expression                  : assignment_expression
                            | expression COMMA assignment_expression
                            ;


postfix_expression          : primary_expression
                            | postfix_expression OB argument_expression_list CB
                            | postfix_expression OB CB
                            | postfix_expression postfix_operator
                            ;
                            
                            
postfix_operator            : UNARY_INCREMENT_OP
                            | UNARY_DECREMENT_OP
                            ;


primary_expression          : ID
                            | const
                            | STRING
                            | OB expression CB
                            ;


argument_expression_list    : assignment_expression
                            | argument_expression_list COMMA assignment_expression
                            ;


const                       : CHAR_CONST
                            | INT_CONST
                            | FLOAT_CONST
                            ;


unary_operator              : AND_OP
                            | MUL_OP
                            | ADD_OP
                            | SUB_OP
                            | BITWISE_NOT_OP
                            | NOT_OP
                            ;


%%


void start_compiling(const char *input, id<CCOutput> output)
{
#if BISON_DEBUG
    yydebug = 1;
#endif
    yylineno = 0;
    _output = output;
    YY_BUFFER_STATE buffer=yy_scan_string(input);
    yyparse();
    yy_delete_buffer(buffer);
}


int yyerror(char *s)
{
    [_output printError:[NSString stringWithFormat:@"[line %i] error: %s\n", yylineno, s]];
    return 0;
}